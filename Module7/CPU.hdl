// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    // A-instruction
    // load A
    Not(in=instruction[15], out=isAInst); // (instruction[15] == 0) <- This is an A instruction
    And(a=instruction[15], b=instruction[5], out=isCAndDestA); // OR (instruction[15] == 1 AND destA == 1) <- C Instruction but save to A
    Or(a=isAInst, b=isCAndDestA, out=loadA);
    Mux16(a=instruction, b=outAlu, sel=instruction[15], out=Ain);
    
    // A Register
    ARegister(in=Ain, load=loadA, out=Aregister, out[0..14]=addressM);

    // load D
    // only when destD = 1 and C-Instruction
    And(a=instruction[15], b=instruction[4], out=loadD);

    // D Register
    DRegister(in=outAlu, load=loadD, out=Dregister);

    // to ALU
    Mux16(a=Aregister, b=inM, sel=instruction[12], out=AluY);
    ALU(x= Dregister, y=AluY , zx= instruction[11], nx= instruction[10], zy= instruction[9], ny= instruction[8], f= instruction[7], no= instruction[6], out= outAlu, out= outM, zr= zr, ng= ng);
    And(a= instruction[15], b= instruction[3], out= writeM);

    // zr, if (out == 0) equals 1, else 0
    // ng, if (out < 0)  equals 1, else 0
    
    Not(in= zr, out= NotZr);
    Not(in= ng, out= NotNg);
    // JGT: out > 0
    And(a= NotZr, b= NotNg, out= JGT);
    // JEQ: out == 0
    // zr
    // JGE: out >= 0
    // NotNg
    // JLT: out < 0
    // ng
    // JNE: out != 0
    // NotZr
    // JLE: out <= 0
    Or(a=zr, b=ng, out=JLE);
    // JMP: Unconditional jump

    And(a=instruction[0], b=JGT, out=jump1); // JGT
    And(a=instruction[1], b=zr,  out=jump2); // JEQ
    And(a=instruction[2], b=ng,  out=jump3); // JLT 

    // check for the 1 position: 001, 010, 100. It should contain all the situation.

    Or(a=jump1, b=jump2, out=t1);
    Or(a=t1, b=jump3, out=jumpCond);
    
    And(a=instruction[15], b=jumpCond, out=loadPC);
    Not(in= loadPC, out= inc);
    // PC
    PC(in= Aregister, load= loadPC, inc= inc, reset= reset, out[0..14]= pc);
}